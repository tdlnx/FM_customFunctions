// Author: Travis Witt c.2021
// Purpose: Take your boring old text and make it zesty
//          ~ Now with 3 options from ChIlLy to SpIcY!
// Parameters: boringText (text), zestLevel (int [1-3])
// Notes: As a special added bonus, entering '0' for
//        zestLevel returns chilly cold text ~bRrRrr~
//        Also, if no zest level is specified 'med' is
//        selected by default.
// ===
// Changelog: 
// 03/21/2021 [Travis]: Created
// ===

Let ([
    ~text = boringText;
    ~zest = Case ( 
                zestLevel = 0; "col";
                zestLevel = 1; "mil";
                zestLevel = 2; "med";
                zestLevel = 3; "hot";
                "med"
            );
    ~length = Length(~text);
    ~ingredients = While ([
                    ~i = 1;
                    ~list = ""
                ];
                    ~i <= ~length;
                [
                    ~random = Int(Random * 4) + 1;
                    ~list = List (
                                ~list;
                                ~zest & ~random;
                            );
                    ~i = ~i + 1
                ];
                    ~list
                )
];
    While ([
                ~i = 1;
                ~salsa = ""
            ];
                ~i <= ~length;
            [
                ~salsa = ~salsa & TextColor ( Middle ( ~text; ~i; 1 ); 
                                                    Case(
                                                        //SpIcY
                                                        GetValue ( ~ingredients; ~i) = "hot1"; RGB(250; 30; 14);
                                                        GetValue ( ~ingredients; ~i) = "hot2"; RGB(140; 0; 0);
                                                        GetValue ( ~ingredients; ~i) = "hot3"; RGB(250; 30; 14);
                                                        GetValue ( ~ingredients; ~i) = "hot4"; RGB(190; 0; 0);
                                                        //MeDiUm
                                                        GetValue ( ~ingredients; ~i) = "med1"; RGB(226; 112; 58);
                                                        GetValue ( ~ingredients; ~i) = "med2"; RGB(228; 137; 0);
                                                        GetValue ( ~ingredients; ~i) = "med3"; RGB(255; 201; 60);
                                                        GetValue ( ~ingredients; ~i) = "med4"; RGB(190; 0; 0);
                                                        //MiLd
                                                        GetValue ( ~ingredients; ~i) = "mil1"; RGB(158; 222; 115);
                                                        GetValue ( ~ingredients; ~i) = "mil2"; RGB(255; 201; 60);
                                                        GetValue ( ~ingredients; ~i) = "mil3"; RGB(190; 202; 92);
                                                        GetValue ( ~ingredients; ~i) = "mil4"; RGB(117; 207; 184);
                                                        //ChIlLy
                                                        GetValue ( ~ingredients; ~i) = "col1"; RGB(49; 50; 111);
                                                        GetValue ( ~ingredients; ~i) = "col2"; RGB(0; 117; 128);
                                                        GetValue ( ~ingredients; ~i) = "col3"; RGB(120; 196; 212);
                                                        GetValue ( ~ingredients; ~i) = "col4"; RGB(4; 101; 130);
                                                    )
                                            );
                ~i = ~i + 1
            ];
                ~salsa
            )
)