/**
 *******************************************************************************
 * VirtualValueListSet ( displayList ; idList ; virtualValueListID )
 *
 * PURPOSE:
 *		Sets a virtual value list with a specified series of return-delimited
 *		values. This function works in connection with a value list defined to
 *		be based on an unstored calculation that references the global variables
 *		set by this function.
 *
 * RETURNS: True (1)
 *
 * PARAMETERS:
 *		displayList: A return-delimited list of values to display, in the order
 *		they are to be displayed. Clears the value list when left empty.
 *		idList: (optional) A return-delimited list of values to set from the
 *		value list, but not display, in matching order with displayList.
 *		virtualValueListID: (optional) The unique (numeric) ID of the dynamic
 *		value list to address. It is the responsibility of the solution
 *		developer to devise and adhere to a consistent ID scheme. Defaults to 1.
 *
 * DEPENDENCIES: none
 *
 * VARIABLES:
 *		$$~VirtualValueList.DISPLAY_LIST[n]
 *		$$~VirtualValueList.ID_LIST[n]
 *
 * NOTES:
 *		This module is based on techniques demonstrated by John Ahn,
 *		Andries Heylen, and Marcelo Piñeyro.
 *
 * HISTORY:
 *		MODIFIED on 2013-05-18 by Jeremy Bante <http://scr.im/jbante> to use the
 *		displayList without prepended non-breaking spaces as the idList if the
 *		idList parameter is empty to deal with problems where non-breaking
 *		spaces are preserved in selected values.
 *		CREATED on 2013-05-06 by Jeremy Bante <http://scr.im/jbante>.
 *
 * REFERENCES:
 *		http://www.filemakerhacks.com/?p=5357
 *		http://www.filemakerhacks.com/?p=5412
 *		http://www.soliantconsulting.com/blog/2012/09/extending-filemaker-pro’s-value-list-sort-capabilities-using-char-function
 *******************************************************************************
 */

Case (
	/* Step 0, set-up */
	not $~vvl.step ;
		Let ( [
			~virtualValueListID =
				If ( IsEmpty ( virtualValueListID ) ;
					1 ;
					/* Else */ virtualValueListID
				) ;
			$$~VirtualValueList.DISPLAY_LIST[~virtualValueListID] = "" ;
			$~vvl.valueCount = ValueCount ( displayList ) ;
			$~vvl.nonBreakingSpace = Char ( 65279 ) ;
			$~vvl.step = 1
		] ;
			VirtualValueListSet ( displayList ; idList ; ~virtualValueListID )
		) ;

	/* Step 1, append non-breaking spaces to line starts to coerce sort order */
	$~vvl.step = 1 ;
		Let ( [
			$~vvl.i = $~vvl.i + 1 ;
			$$~VirtualValueList.DISPLAY_LIST[virtualValueListID] =
				If ( $~vvl.i > 1 ;
					$$~VirtualValueList.DISPLAY_LIST[virtualValueListID] & ¶
				)
				& $~vvl.prefix
				& GetValue ( displayList ; $~vvl.i ) ;
			$~vvl.prefix = $~vvl.prefix & $~vvl.nonBreakingSpace ;
			$~vvl.step =
				If ( $~vvl.i < $~vvl.valueCount ;
					$~vvl.step ;
					/* Else */ $~vvl.step + 1
				)
		] ;
			VirtualValueListSet ( displayList ; idList ; virtualValueListID )
		) ;

	/* Step 2, clean-up and return result */
	$~vvl.step = 2 ;
		Let ( [
			$$~VirtualValueList.ID_LIST[virtualValueListID] =
				Case (
					not IsEmpty ( idList ) and $~vvl.valueCount > 0 ;
						idList ;
					$~vvl.valueCount > 0 ;
						displayList
				) ;

			// purge variables
			$~vvl.i = "" ;
			$~vvl.nonBreakingSpace = "" ;
			$~vvl.prefix = "" ;
			$~vvl.step = "" ;
			$~vvl.valueCount = ""
		] ;
			True
		)
)